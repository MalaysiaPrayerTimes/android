apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'hu.supercluster.paperwork'
apply plugin: 'com.github.triplet.play'
apply from: 'signing.gradle'
apply from: '../jacoco.gradle'
apply from: '../checkstyle.gradle'

def IS_CI = "true".equals(System.getenv("CI"))

def versionMajor = 2
def versionMinor = 0
def versionPatch = 3
def versionBuild = (System.getenv('TRAVIS_BUILD_NUMBER') ?: 0) as Integer
def sumVersionCode = versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    flavorDimensions "default"

    dexOptions {
        preDexLibraries = !IS_CI
    }

    defaultConfig {
        applicationId "com.i906.mpt"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode sumVersionCode
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        resValue 'string', 'foursquare_id', System.getenv('MPT_FOURSQUARE_ID') ?: MPT_FOURSQUARE_ID
        resValue 'string', 'foursquare_secret', System.getenv('MPT_FOURSQUARE_SECRET') ?: MPT_FOURSQUARE_SECRET
        resValue 'string', 'gcm_defaultSenderId', System.getenv('MPT_GCM_SENDER_ID') ?: MPT_GCM_SENDER_ID
        resValue 'string', 'google_analytics_id', System.getenv('MPT_GOOGLE_ANALYTICS_ID') ?: MPT_GOOGLE_ANALYTICS_ID
        resValue 'string', 'google_app_id', System.getenv('MPT_GOOGLE_APP_ID') ?: MPT_GOOGLE_APP_ID
        resValue 'string', 'google_api_key', System.getenv('MPT_GOOGLE_API_KEY') ?: MPT_GOOGLE_API_KEY
        resValue 'string', 'google_crash_reporting_api_key', System.getenv('MPT_GOOGLE_CRASH_API_KEY') ?: MPT_GOOGLE_CRASH_API_KEY
        resValue 'string', 'facebook_app_id', System.getenv('MPT_FACEBOOK_APP_ID') ?: MPT_FACEBOOK_APP_ID

        manifestPlaceholders = [
                MPT_FABRIC_API_KEY: System.getenv('MPT_FABRIC_API_KEY') ?: MPT_FABRIC_API_KEY
        ]

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled IS_CI

            def key = System.getenv('MPT_FABRIC_API_KEY') ?: MPT_FABRIC_API_KEY
            if (!key) ext.enableCrashlytics = false
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        mock {
            dimension "default"
        }

        beta {
            dimension "default"
            versionNameSuffix '-beta'
            versionCode(sumVersionCode + 5)
        }

        prod {
            dimension "default"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            exceptionFormat 'full'
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "mpt-${variant.versionName}-${variant.productFlavors[0].name}-${variant.buildType.name}.apk"
        }
    }

    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }
}

paperwork {
    set = [
            buildTime: buildTime("yyyy-MM-dd HH:mm:ss", "GMT+0800"),
            gitInfo  : gitInfo()
    ]
}

play {
    jsonFile = file('0c3c7fc227f6.json')
    track = System.getProperty("PLAY_STORE_TRACK", PLAY_STORE_TRACK)
    userFraction = 0.2

    // https://github.com/Triple-T/gradle-play-publisher/issues/162
    // untrackOld = true
}

dependencies {
    def BUTTERKNIFE_VERSION = rootProject.ext.BUTTERKNIFE_VERSION
    def DAGGER_VERSION = rootProject.ext.DAGGER_VERSION
    def DEXMAKER_VERSION = '1.4'
    def ESPRESSO_VERSION = '2.2.2'
    def FIREBASE_VERSION = rootProject.ext.FIREBASE_VERSION
    def GSON_VERSION = rootProject.ext.GSON_VERSION
    def MOCKITO_VERSION = rootProject.ext.MOCKITO_VERSION
    def OKHTTP_VERSION = rootProject.ext.OKHTTP_VERSION
    def PLAYSERVICES_VERSION = rootProject.ext.PLAYSERVICES_VERSION
    def RXANDROID_VERSION = rootProject.ext.RXANDROID_VERSION
    def RXJAVA_VERSION = rootProject.ext.RXJAVA_VERSION
    def STETHO_VERSION = '1.5.0'
    def STORIO_VERSION = rootProject.ext.STORIO_VERSION
    def SUPPORT_VERSION = rootProject.ext.SUPPORT_VERSION
    def TIMBER_VERSION = rootProject.ext.TIMBER_VERSION

    implementation project(':api-client')
    implementation project(':mpt-extension')

    // Android Support Libraries
    implementation "com.android.support:appcompat-v7:$SUPPORT_VERSION"
    implementation "com.android.support:cardview-v7:$SUPPORT_VERSION"
    implementation "com.android.support:design:$SUPPORT_VERSION"

    // Play Service
    implementation "com.google.android.gms:play-services-location:15.0.1"
    implementation "com.google.android.gms:play-services-analytics:16.0.3"

    // Firebase
    implementation "com.google.firebase:firebase-analytics:16.0.3"

    // Butterknife
    implementation "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    annotationProcessor "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"

    // OkHttp Logging
    implementation "com.squareup.okhttp3:logging-interceptor:$OKHTTP_VERSION"

    // Dagger
    implementation "com.google.dagger:dagger:$DAGGER_VERSION"
    annotationProcessor "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    compileOnly 'javax.annotation:jsr250-api:1.0'

    // RX
    implementation "io.reactivex:rxjava:$RXJAVA_VERSION"
    implementation "io.reactivex:rxandroid:$RXANDROID_VERSION"

    // Database
    implementation "com.pushtorefresh.storio:sqlite:$STORIO_VERSION"
    implementation "com.pushtorefresh.storio:content-resolver:$STORIO_VERSION"

    // Stetho
    debugImplementation "com.facebook.stetho:stetho:$STETHO_VERSION"
    debugImplementation "com.facebook.stetho:stetho-okhttp3:$STETHO_VERSION"
    debugImplementation "com.facebook.stetho:stetho-timber:$STETHO_VERSION"

    // Stuffs
    implementation "com.google.code.gson:gson:$GSON_VERSION"

    // Misc
    implementation 'com.github.msarhan:ummalqura-calendar:1.1.7'
    implementation 'com.heinrichreimersoftware:material-intro:1.6'
    implementation 'com.github.frankiesardo:linearlistview:1.0.1@aar'
    implementation "com.jakewharton.timber:timber:$TIMBER_VERSION"
    implementation 'hu.supercluster:paperwork:1.2.7'
    implementation 'net.yslibrary.licenseadapter:licenseadapter:1.2.4'
    implementation 'com.facebook.android:facebook-core:[4,5)'
    implementation 'com.github.gabrielemariotti.changeloglib:changelog:2.1.0'

    implementation 'com.github.kizitonwose.colorpreference:core:1.0.2'
    implementation 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    implementation 'com.github.AndroidDeveloperLB:AutoFitTextView:4'
    implementation 'uk.co.chrisjenx:calligraphy:2.2.0'

    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-all:$MOCKITO_VERSION"
    testImplementation "org.mockito:mockito-core:$MOCKITO_VERSION"
    testImplementation 'org.assertj:assertj-core:1.7.1'

    androidTestImplementation 'com.github.fabioCollini:DaggerMock:0.6.2'
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    androidTestImplementation "com.crittercism.dexmaker:dexmaker:$DEXMAKER_VERSION"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker-dx:$DEXMAKER_VERSION"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker-mockito:$DEXMAKER_VERSION"

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation "com.android.support:support-annotations:$SUPPORT_VERSION"

    androidTestImplementation('com.android.support.test.espresso:espresso-core:$ESPRESSO_VERSION', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$ESPRESSO_VERSION"

    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$ESPRESSO_VERSION") {
        exclude module: 'support-v4'
        exclude module: 'recyclerview-v7'
        exclude module: 'appcompat-v4'
        exclude module: 'design'
    }

    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }
}

configurations.all {
    resolutionStrategy {
        force "com.android.support:support-annotations:${rootProject.ext.SUPPORT_VERSION}"
    }
}

task grantAnimationPermission(type: Exec, dependsOn: 'installMockDebug') {
    commandLine "adb shell pm grant com.i906.mpt.staging android.permission.SET_ANIMATION_SCALE".split(' ')
}

tasks.whenTaskAdded { task ->
    if (task.name.startsWith('connected')) {
        task.dependsOn grantAnimationPermission
    }
}
